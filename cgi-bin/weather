#!/usr/bin/perl

# weather: RRD CGI graphing frontend by twitham@sbcglobal.net
# see wview-rrd for the backend data storage job, 2013/01

use strict;
use CGI qw/:all/;
use CGI::Carp qw(fatalsToBrowser);
our($inc, $basename) = ($0 =~ m@(.*)/(.*)$@);
require "$inc/rrdgraphcgi.pl";

# ------------------------------------------------------------

our $where = "/var/lib/wview/archive"; # path to RRD files
our $wview	= "wview.rrd";	# main RRD file
our $wind	= "wind.rrd";	# wind directions
our $rain	= "rain.rrd";	# rain total
our $title	= 'Wview Weather'; # title for the page
our $primary	= 'FF0000';	# primary metric color
our $secondary	= '0000FF';	# secondary metric color
our $third	= '008800';	# third metric color
our $indoor	= 'FF8000';	# indoor metric color
our $name1	= 'Extra1';	# one-word name for:
our $color1	= '000000';	#   extra sensor 1
our $name2	= 'Extra2';	# one-word name for:
our $color2	= '888888';	#   extra sensor 2
our $link	= '';		# optional header HTML, like:
# $link = ' See also: '. a({-href => '/weather/'}, 'wview page');


$ENV{PATH} = "/usr/bin:$ENV{PATH}";

# override anything above in configuation file
-f "$0.pl" and ! -w  and require "$0.pl";
# ------------------------------------------------------------

our $rrd;			# array of graph references
our($width, @onchange);		# set by rrdgraphcgi.pl

$| = 1;
chdir $where or die $!;
&rrdinit();

if (param('footer')) {
    print header, start_html,
    "<table><tr valign=top><td>\n";
    for my $file ($wview, $wind, $rain) {
	print "<td>";
	&rrdfooter($file) if -f $file;
	print "\n";
    }
    print "</tr></table>\n";
    print end_html;
    exit;
}

my $tr = param('column') ? "<tr><td>" : "";

push @$rrd, "<tr><td>", &outtemp;
push @$rrd, &intemp if param('indoor');
push @$rrd, $tr, &humidity;
push @$rrd, &inhumidity if param('indoor');
push @$rrd, $tr, &wind,
    checkbox(-name => 'count', @onchange,
	     -title => 'direction counts rather than percents',
	     -label => '');
push @$rrd, $tr, &barometer,
    checkbox(-name => 'pressure', @onchange,
	     -title => 'add local pressure',
	     -label => '');
push @$rrd, $tr, &rain;
push @$rrd, $tr, &rainrate;
#push @$rrd, $tr, &uv;

if (my $graph = param('graph')) {

    &rrdgraph(@{$rrd->[$graph]});

} else {

    &rrdheader($title, undef, undef,
	       [$link ? $link : ''],
	       undef,
	       [checkbox(-name => 'column', @onchange,
			 -title => 'show graphs in columns'),
		checkbox(-name => 'inout', @onchange,
			 -title => 'add indoor metrics to outdoor graphs'),
		checkbox(-name => 'indoor', @onchange,
			 -title => 'add indoor graphs'),
		(param('inout') || param('indoor') ?
		 (checkbox(-name => lc $name1, @onchange,
			   -title => "add $name1 sensor"),
		  checkbox(-name => lc $name2, @onchange,
			   -title => "add $name2 sensor")) : ()),
	       ]);

    if (my $g = param('graphtime')) {
	&rrdgraphtime($g);
    } else {
	&rrdtable($rrd);
    }

    print qq'<hr class="footer">\ngenerated from ',
    &link("3 rrd files", { footer => 1}, 'RRD Info'),
    ", last modified ", scalar(localtime((stat $wview)[9])),
    " by ", a({-href => 'https://github.com/twitham1/wview-rrd'},
	      'wview-rrd'), "\n";

    print end_form, end_html;
}

sub outtemp {
    return [
	'Temperature', qw(-Y -v), 'Degrees Fahrenheit',
	(param('inout')
	 ? ((param(lc $name2) ?
	     &range($wview, 'extraTemp2',
		    sprintf('%-11s', $name2), $color2) : ()),
	    (param(lc $name1) ?
	     &range($wview, 'extraTemp1',
		    sprintf('%-11s', $name1), $color1) : ()),
	    &range($wview, 'inTemp',     'INDOOR     ', $indoor))
	 :()),
	&range($wview, 'heatindex', 'Heat Index ', $secondary),
	&range($wview, 'windchill', 'Wind Chill ', $secondary),
	&range($wview, 'dewpoint',  'Dew Point  ', $third),
	&range($wview, 'outTemp',   'Temperature', $primary),
	(param('inout')
	 ? "LINE2:avginTemp#$indoor"
	 : ()),
	"HRULE:32#0000FF88",	# freeze line
	"LINE2:avgheatindex#$secondary",
	"LINE2:avgwindchill#$secondary",
	"LINE2:avgdewpoint#$third",
	"LINE2:avgoutTemp#$primary",
	];
}

sub intemp {
    return [
	'INDOOR Temperature', qw(-Y -v), 'Degrees Fahrenheit',
	(param(lc $name2) ?
	 &range($wview, 'extraTemp2',
		sprintf('%-6s Temperature', $name2), $color2) : ()),
	(param(lc $name1) ?
	 &range($wview, 'extraTemp1',
		sprintf('%-6s Temperature', $name1), $color1) : ()),
	&range($wview, 'inTemp',     'INDOOR Temperature', $indoor),
	"HRULE:32#0000FF88",	# freeze line
	];
}

sub humidity {
    return [
	'Humidity', qw(-Y -v), 'Percent',
	(param('inout')
	 ? ((param(lc $name2) ?
	     &range($wview, 'extraHumid2',
		    sprintf('%-8s', $name2), $color2) : ()),
	    (param(lc $name1) ?
	     &range($wview, 'extraHumid1',
		    sprintf('%-8s', $name1), $color1) : ()),
	    &range($wview, 'inHumidity',  'INDOOR  ', $indoor))
	 :()),
	&range($wview, 'outHumidity',   'Humidity', $primary),
	];
}

sub inhumidity {
    return [
	'INDOOR Humidity', qw(-Y -v), 'Percent',
	(param(lc $name2) ?
	 &range($wview, 'extraHumid2',
		sprintf('%-6s Humidity', $name2), $color2) : ()),
	(param(lc $name1) ?
	 &range($wview, 'extraHumid1',
		sprintf('%-6s Humidity', $name1), $color1) : ()),
	&range($wview, 'inHumidity',  'INDOOR Humidity', $indoor),
	];
}

# Wind direction AVERAGE is meaningless.  So instead, we plot the most
# common wind direction as a color code below the X axis, for both
# gust and average direction.
sub wind {
    my @wind = (
	'Wind', qw(-u 10 -Y -v), 'Miles Per Hour',
	&range($wview, 'windGust',  'Gust   ', $secondary, 0,
	       "COMMENT:Dir ==  0/ -5"),
	&range($wview, 'windSpeed', 'Average', $primary, 0,
	       "COMMENT:Dir == -5/-10"),
	"LINE2:avgwindGust#$secondary",
	"LINE2:avgwindSpeed#$primary",
	);
    my @rose = qw(nw n ne w 0 e sw s se); # wind compass buckets in 3x3 order
    my @dir = grep $_, @rose;	# directions only (no center), colors:
    my %dir = qw(
	nw 0000FF	n 00FFFF	ne 00FF00
	w  800080	0 FFFFFF	e  FFFF00
	sw FF00FF	s FF0000	se FF8000);
    my @doc = ("COMMENT:            (N = cold = blues)\\l",
	       "COMMENT:(W = sunset = dark) * (E = sunrise = bright)\\l",
	       "COMMENT:             (S = hot = reds)\\l");
    for (@dir) {
	push @wind, "DEF:g$_=$wind:g$_:AVERAGE";
	push @wind, "DEF:a$_=$wind:a$_:AVERAGE";
    }
    push @wind, join ',', "CDEF:gmax=0", map { "g$_,MAX" } @dir;
    push @wind, join ',', "CDEF:amax=0", map { "a$_,MAX" } @dir;
    push @wind, join ',', "CDEF:gtot=0", map { "g$_,+" } @dir;
    push @wind, join ',', "CDEF:atot=0", map { "a$_,+" } @dir;
    my $i = 0;
    my @later;	       # defer gust directions to overlay all averages
    for (@rose) {		# 3x3 compass rose directions
	$i++;
	my $color = $dir{$_};
	unless ($_) {		# rose center is just legend labels
	    my $chr = param('count') ? '#' : '%';
	    push @wind, "CDEF:null=gmax,0,MIN";
	    push @wind, "AREA:null#$color: ";
	    push @wind, "COMMENT:Gust$chr";
	    push @wind, "COMMENT:Average$chr";
	    next;
	}
	# plot only the most common bucket direction as an area
	my $chr = length($_) < 2 ? ' ' : '';
	push @wind, "CDEF:gplot$_=g$_,0,GT,g$_,gmax,GE,-5,0,IF,0,IF";
	push @wind, "CDEF:aplot$_=a$_,0,GT,a$_,amax,GE,-10,0,IF,0,IF";
	push @wind, "AREA:aplot$_#$color:\U$_$chr";
	push @later, "AREA:gplot$_#$color"; # defer gust overlay
	if (param('count')) {	# bucket counts in the legend
	    push @wind, "VDEF:totg$_=g$_,TOTAL";
	    push @wind, "VDEF:tota$_=a$_,TOTAL";
	    push @wind, "GPRINT:totg$_:%6.0lf";
	    push @wind, "GPRINT:tota$_:%6.0lf";
	} else {		# bucket percentages in the legend
	    push @wind, "CDEF:gpct$_=g$_,gtot,/,100,*";
	    push @wind, "VDEF:pctg$_=gpct$_,AVERAGE";
	    push @wind, "CDEF:apct$_=a$_,atot,/,100,*";
	    push @wind, "VDEF:pcta$_=apct$_,AVERAGE";
	    push @wind, "GPRINT:pctg$_:%5.1lf%%";
	    push @wind, "GPRINT:pcta$_:%5.1lf%%";
	}
	$i % 3 and next;
	push @wind, ($width > 600 ? (shift @doc) : "COMMENT:\\l");
    }
    push @wind, @later;	# gust directions overlay now that average is done
    return [ @wind ];
}

sub barometer {
    return [
	'Barometer', qw(-Y -v), 'Inches of Mercury',
	(param('pressure')
	 ? &range($wview, 'pressure',  'Pressure ', $third)
	 : ()),
	&range($wview, 'altimeter', 'Altimeter', $secondary),
	&range($wview, 'barometer', 'Barometer', $primary),
	"HRULE:29.92#0000FF88",	# standard atmosphere
	"HRULE:29.53#0000FF88",	# 1 bar = standard pressure
	"LINE2:avgaltimeter#$secondary",
	"LINE2:avgbarometer#$primary",
	];
}

sub rain {
    return [
	'Rain', qw(-Y -v), 'Inches',
	&range($rain, 'totalRain', 'Rain', $primary),

	"CDEF:rain=lasttotalRain,UN,PREV,lasttotalRain,IF", # U->same
	"CDEF:delta=rain,PREV(rain),UN,rain,PREV(rain),IF,-", # change
	"CDEF:add=delta,-0.0001,LT,rain,delta,IF", # counter reset
	"CDEF:sum=PREV,UN,0,PREV,IF,add,+", # running sum of rain

	# "CDEF:rain=lasttotalRain,UN,PREV,lasttotalRain,IF", # U->same
	# "CDEF:delta=rain,PREV(rain),UN,rain,PREV(rain),IF,-", # change
	# "CDEF:alt=maxtotalRain,mintotalRain,-",		      # range
	# "CDEF:add=delta,-0.0001,LT,rain,delta,alt,MAX,IF", # counter reset
	# "CDEF:sum=PREV,UN,0,PREV,IF,add,+", # running sum of rain

	"VDEF:sumRain=sum,LAST",
	"GPRINT:sumRain:(%.2lf inches of Rain to",
	"GPRINT:sumRain:%a %b %d %H\\:%M %Y)\\l:strftime",
	];
}

sub rainrate {
    return [
	'Rain Rate', qw(-Y -v), 'Inches Per Hour',
	&range($wview, 'rainRate', 'Rain Rate', $primary),
	];
}

sub uv {
    return [
	'Ultra Violet', qw(-Y -v), 'Ultra Violet Index',
	&range($wview, 'UV', 'Ultra Violet', $primary),
	];
}

BEGIN {
    my $offset = 0;			# dash-offset
# return RRD code to plot range of given $var from $file in $color
sub range {
    my($file, $var, $label, $color, $alpha, $more) = @_;
    $label = $var unless $label;
    $color = '000000' unless $color;
    $alpha = '88' unless $alpha;
    $offset += 3; $offset = 0 if $offset > 12;
    return (
	"DEF:avg$var=$file:$var:AVERAGE",
	"VDEF:avgavg$var=avg$var,AVERAGE",
	"DEF:min$var=$file:$var:MIN",
	"VDEF:minmin$var=min$var,MINIMUM",
	"DEF:max$var=$file:$var:MAX",
	"VDEF:maxmax$var=max$var,MAXIMUM",
	"DEF:last$var=$file:$var:LAST",
	"VDEF:lastlast$var=last$var,LAST",

	"CDEF:diff$var=max$var,min$var,-",

	"VRULE:minmin$var#${color}${alpha}::dashes=3,9:dash-offset=$offset",
	"VRULE:maxmax$var#${color}${alpha}::dashes=3,9:dash-offset=$offset",
	"HRULE:avgavg$var#${color}${alpha}::dashes=3,9:dash-offset=$offset",
	"LINE1:min$var#${color}00",
	"AREA:diff$var#${color}${alpha}::STACK",
	"LINE2:avg$var#${color}:$label",
	"GPRINT:minmin$var:(%6.2lf  <",
	"GPRINT:avgavg$var:%6.2lf  <",
	"GPRINT:maxmax$var:%6.2lf)",
	"GPRINT:lastlast$var:%6.2lf",
	($more ? $more : ()),
	($width > 600
	 ? ("GPRINT:minmin$var:(%a %b %d %H\\:%M %Y  <:strftime",
	    "GPRINT:maxmax$var:%a %b %d %H\\:%M %Y)\\l:strftime")
	 : ("COMMENT:\\l")),
	);
}
}
